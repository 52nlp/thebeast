package thebeast.pml.parser;

import java.util.LinkedList;
import java_cup.runtime.*;
import thebeast.pml.*;
import thebeast.pml.formula.*;
import thebeast.pml.term.*;

parser code {:

  public void syntax_error(Symbol cur_token){
    throw new PMLParseException("Syntax error", cur_token);
  }
:}

terminal TYPE,PREDICATE, WEIGHT, X, IF, FOR, ADD, UNDERSCORE, INSPECT, ATOMS, GOLD, CORPUS, FROM, PRINT;
terminal SOLVE, LEARN, TYPES, TO, ELLIPSIS, JUMP, FACTOR, ACYCLIC, SCORES, GREEDY, WEIGHTS;
terminal COLLECT, ARG, SEQ, EPOCHS, SET, CLEAR, BINS;
terminal String LOWERCASEID, UPPERCASEID;
terminal INF, INCLUDE, DOUBLETYPE,INTTYPE, HIDDEN, OBSERVED;
terminal DOUBLESEMI, SEMI, UMINUS, UAND, COMMA, ASSIGN, DOUBLEDOT, COLON, DOT;
terminal PLUS, MINUS, TIMES;
terminal EQUALS, NOTEQUALS, LESSTHAN, GREATER, GEQ, LEQ;
terminal EXCL;
terminal TILDE, HASH, AT;
terminal EQUIVALENCE, AND, IMPLIES, DOUBLEAND, DOUBLEBAR;
terminal LPAREN, RPAREN, LSQPAREN, RSQPAREN, LCLPAREN, RCLPAREN, LCLPARENSTAR, RCLPARENSTAR;
terminal LCLPARENPLUS, RCLPARENPLUS, LCLPARENEXCL, RCLPARENEXCL;
terminal BAR,ARROW,CURLYARROW;
terminal FUNCTION,LET,WHEN,ASSERT,IN,OF,LAMBDA,DOLLAR;
terminal TRUE,FALSE;
terminal JAVA;
terminal INVERT;
terminal PUT,MAP,WITH;
terminal WHILE,DO;
terminal NULL;
terminal Integer NUMBER;
terminal Double DOUBLE;
terminal String IDENT,STRING,JAVAID,ARGIDENT;

non terminal LinkedList<ParserStatement> begin;
non terminal LinkedList<ParserStatement> program;
non terminal LinkedList<ParserConstant> constantlist;
non terminal LinkedList<ParserNamedConstant> namedconstantlist;
non terminal LinkedList<ParserTerm> arguments;
non terminal LinkedList<ParserTyping> quantification;
non terminal ParserTyping typing;
non terminal LinkedList<String> argtypes, lowercasecommalist;
non terminal ParserTerm constant,term;
non terminal ParserNamedConstant namedconstant;
non terminal ParserFormula atom, formula;
non terminal LinkedList<ParserFormula> conjunction;
non terminal ParserFactorFormula factor;
non terminal ParserIntConstant intconstant;
non terminal ParserStatement statement;
non terminal ParserCreateType createType;
non terminal ParserCreatePredicate createPredicate;
non terminal ParserCreateWeightFunction createWeightFunction;

precedence nonassoc ACYCLIC;
precedence nonassoc FOR, ADD, IF, LSQPAREN, RSQPAREN;
precedence left COMMA;
precedence left X;
precedence nonassoc LPAREN,RPAREN,UNDERSCORE;
precedence left IMPLIES;
precedence left AND;
precedence left PLUS, MINUS;

start with begin;

begin ::= program:p SEMI
          {: RESULT = p; :}
        |
          {: RESULT = new LinkedList<ParserStatement>(); :}
          ;

program ::= program:program SEMI statement:stmt
            {: program.add(stmt);
               RESULT = program; :}
          | statement:stmt
            {: LinkedList<ParserStatement> statements = new LinkedList<ParserStatement>();
               statements.add(stmt);
               RESULT = statements; :};

statement ::= createType:s
              {: RESULT = s; :}
            | createPredicate:s
              {: RESULT = s; :}
            | createWeightFunction:s
              {: RESULT = s; :}
            | factor:f
              {: RESULT = f; :}
            | INCLUDE STRING:s
              {: RESULT = new ParserImport(s.substring(1,s.length()-1)); :}
            | HIDDEN COLON lowercasecommalist:a
              {: RESULT = new ParserAddPredicateToModel(ParserAddPredicateToModel.Type.HIDDEN,a); :}
            | OBSERVED COLON lowercasecommalist:a
              {: RESULT = new ParserAddPredicateToModel(ParserAddPredicateToModel.Type.OBSERVED,a); :}
            | INSPECT LOWERCASEID:id
              {: RESULT = new ParserInspect(id, false); :}
            | INSPECT UPPERCASEID:id
              {: RESULT = new ParserInspect(id, true); :}
            | PRINT ATOMS LOWERCASEID:id
              {: RESULT = new ParserPrint(id,false,false); :}
            | PRINT SCORES
              {: RESULT = new ParserPrint(null,false,true); :}
            | PRINT ATOMS
              {: RESULT = new ParserPrint(null,false,false); :}
            | PRINT WEIGHTS
              {: RESULT = new ParserPrintWeights(null); :}
            | PRINT WEIGHTS LOWERCASEID:function
              {: RESULT = new ParserPrintWeights(function); :}
            | ATOMS FROM STRING:s
              {: RESULT = new ParserLoad(null, s.substring(1,s.length()-1),false,false); :}
            | SCORES FROM STRING:s
              {: RESULT = new ParserLoadScores(s.substring(1,s.length()-1)); :}
            | WEIGHTS FROM STRING:s
              {: RESULT = new ParserLoadWeights(s.substring(1,s.length()-1)); :}
            | CORPUS FROM STRING:s
              {: RESULT = new ParserLoadCorpus(null, s.substring(1,s.length()-1)); :}
            | CORPUS FROM STRING:s LPAREN NUMBER:f MINUS NUMBER:t RPAREN
              {: RESULT = new ParserLoadCorpus(null, s.substring(1,s.length()-1), 
                                               Integer.valueOf(f), Integer.valueOf(t)); :}
            | CORPUS FROM LOWERCASEID:f STRING:s
              {: RESULT = new ParserLoadCorpus(f, s.substring(1,s.length()-1)); :}
            | CORPUS FROM LOWERCASEID:factory STRING:s LPAREN NUMBER:f MINUS NUMBER:t RPAREN
              {: RESULT = new ParserLoadCorpus(factory, s.substring(1,s.length()-1), 
                                               Integer.valueOf(f), Integer.valueOf(t)); :}
            | TYPES FROM LOWERCASEID:g STRING:s
              {: RESULT = new ParserGenerateTypes(g, s.substring(1,s.length()-1)); :}
            | TYPES TO STRING:s
              {: RESULT = new ParserSaveTypes(s.substring(1,s.length()-1)); :}
            | ATOMS LOWERCASEID:id FROM STRING:s
              {: RESULT = new ParserLoad(id, s.substring(1,s.length()-1),false,false); :}
            | SOLVE NUMBER:n
              {: RESULT = new ParserSolve(Integer.valueOf(n)); :}
            | SOLVE
              {: RESULT = new ParserSolve(100); :}
            | GREEDY
              {: RESULT = new ParserGreedy(); :}
            | COLLECT
              {: RESULT = new ParserCollect(); :}
            | LEARN 
              {: RESULT = new ParserLearn(-1,1); :}
            | LEARN FOR NUMBER:n EPOCHS
              {: RESULT = new ParserLearn(-1,Integer.valueOf(n)); :}
            | LEARN NUMBER:i FOR NUMBER:e EPOCHS
              {: RESULT = new ParserLearn(Integer.valueOf(i),Integer.valueOf(e)); :}
            | LEARN NUMBER:i
              {: RESULT = new ParserLearn(Integer.valueOf(i),-1); :}
            | JUMP NUMBER:n
              {: RESULT = new ParserShift(Integer.valueOf(n)); :}
            | SET LOWERCASEID:q DOT LOWERCASEID:v ASSIGN NUMBER:n
              {: RESULT = new ParserSet(q,v, Integer.valueOf(n)); :}
            | SET LOWERCASEID:q DOT LOWERCASEID:v ASSIGN TRUE
              {: RESULT = new ParserSet(q,v, true); :}
            | SET LOWERCASEID:q DOT LOWERCASEID:v ASSIGN FALSE
              {: RESULT = new ParserSet(q,v, false); :}
            | CLEAR ATOMS
              {: RESULT = new ParserClear("atoms"); :}
            | CLEAR SCORES
              {: RESULT = new ParserClear("scores"); :}
              ;

createType ::= TYPE UPPERCASEID:id COLON namedconstantlist:l
               {: RESULT = new ParserCreateType(id,false,l); :}
             | TYPE UPPERCASEID:id COLON ELLIPSIS namedconstantlist:l
               {: RESULT = new ParserCreateType(id,true,l); :}
               ;

createPredicate ::= PREDICATE LOWERCASEID:id COLON argtypes:l
                    {: RESULT = new ParserCreatePredicate(id,l,-1); :}
                  | PREDICATE LOWERCASEID:id COLON argtypes:l LPAREN SEQ COLON NUMBER:seq RPAREN
                    {: RESULT = new ParserCreatePredicate(id,l,Integer.valueOf(seq)); :}
                    ;


createWeightFunction ::= WEIGHT LOWERCASEID:id COLON argtypes:l ARROW UPPERCASEID:r
               {: RESULT = new ParserCreateWeightFunction(id,l,r); :};

createWeightFunction ::= WEIGHT LOWERCASEID:id COLON argtypes:l ARROW UPPERCASEID:r PLUS
               {: RESULT = new ParserCreateWeightFunction(id,l,r+"+"); :};
               
createWeightFunction ::= WEIGHT LOWERCASEID:id COLON argtypes:l ARROW UPPERCASEID:r MINUS
               {: RESULT = new ParserCreateWeightFunction(id,l,r+"-"); :};


namedconstantlist ::=
    namedconstantlist:l COMMA namedconstant:c 
    {: l.add(c);
       RESULT = l ; :}
  | namedconstant:c
    {: LinkedList<ParserNamedConstant> l = new LinkedList<ParserNamedConstant>();
       l.add(c);
       RESULT = l; :};

argtypes ::=
    argtypes:l X UPPERCASEID:c 
    {: l.add(c);
       RESULT = l ; :}
  | UPPERCASEID:c
    {: LinkedList<String> l = new LinkedList<String>();
       l.add(c);
       RESULT = l; :};

lowercasecommalist ::=
    lowercasecommalist:l COMMA LOWERCASEID:c
    {: l.add(c);
       RESULT = l ; :}
  | LOWERCASEID:c
    {: LinkedList<String> l = new LinkedList<String>();
       l.add(c);
       RESULT = l; :};


typing ::=
    UPPERCASEID:type LOWERCASEID:var
    {: RESULT = new ParserTyping(type,var); :}
    ;

quantification ::=
    quantification:l COMMA typing:c
    {: l.add(c);
       RESULT = l ; :}
  | typing:c
    {: LinkedList<ParserTyping> l = new LinkedList<ParserTyping>();
       l.add(c);
       RESULT = l; :}; 

factor ::=
    FACTOR COLON FOR quantification:q IF formula:c ADD LSQPAREN formula:f RSQPAREN TIMES term:w
    {: RESULT = new ParserFactorFormula(q,c,f,w); :}
  | FACTOR COLON FOR quantification:q IF formula:c COLON formula:f
    {: RESULT = new ParserFactorFormula(q,c,f,new ParserDoubleConstant(Double.POSITIVE_INFINITY)); :}
  | FACTOR COLON FOR quantification:q COLON formula:f
    {: RESULT = new ParserFactorFormula(q,null,f,new ParserDoubleConstant(Double.POSITIVE_INFINITY)); :}
  | FACTOR COLON formula:f
    {: RESULT = new ParserFactorFormula(null,null,f,new ParserDoubleConstant(Double.POSITIVE_INFINITY)); :}
    ;

formula ::=
    LOWERCASEID:name LPAREN arguments:args RPAREN
    {: RESULT = new ParserAtom(name,args); :}
  | formula:l AND formula:r
    {: RESULT = new ParserConjunction(l,r); :}
  | formula:l IMPLIES formula:r
    {: RESULT = new ParserImplies(l,r); :}
  | BAR quantification:q COLON formula:f BAR LEQ term:ub
    {: RESULT = new ParserCardinalityConstraint(new ParserIntConstant(Integer.MIN_VALUE),q,f,ub); :}
  | BAR quantification:q COLON formula:f BAR GEQ term:lb
    {: RESULT = new ParserCardinalityConstraint(lb, q, f, new ParserIntConstant(Integer.MAX_VALUE)); :}
  | LOWERCASEID:pred ACYCLIC
    {: RESULT = new ParserAcyclicityConstraint(pred); :}
  | term:lhs LESSTHAN term:rhs
    {: RESULT = new ParserComparison(ParserComparison.Type.LT, lhs, rhs); :}
  | term:lhs GREATER term:rhs
    {: RESULT = new ParserComparison(ParserComparison.Type.GT, lhs, rhs); :}
    ;

conjunction ::=
    conjunction:l AND formula:c
    {: l.add(c);
       RESULT = l ; :}
  | formula:c
    {: LinkedList<ParserFormula> l = new LinkedList<ParserFormula>();
       l.add(c);
       RESULT = l; :};

arguments ::=
    arguments:l COMMA term:c
    {: l.add(c);
       RESULT = l ; :}
  | term:c
    {: LinkedList<ParserTerm> l = new LinkedList<ParserTerm>();
       l.add(c);
       RESULT = l; :};

term ::=
    constant:c
    {: RESULT = c; :}
  | term:l PLUS term:r
    {: RESULT = new ParserAdd(l,r); :}
  | term:l MINUS term:r
    {: RESULT = new ParserMinus(l,r); :}
  | UNDERSCORE
    {: RESULT = new ParserDontCare(); :}
  | LOWERCASEID:name LPAREN arguments:args RPAREN
    {: RESULT = new ParserFunctionApplication(name,args); :}
  | BINS LPAREN arguments:bins COMMA term:arg RPAREN
    {: RESULT = new ParserBins(bins, arg); :}
    ;



constant ::=
    namedconstant:c
    {: RESULT = c; :}
  | LOWERCASEID:v
    {: RESULT = new ParserVariable(v); :}
  | NUMBER:c
    {: RESULT = new ParserIntConstant(Integer.valueOf(c)); :}
  | MINUS NUMBER:c
    {: RESULT = new ParserIntConstant(-Integer.valueOf(c)); :}
  | DOUBLE:c
    {: RESULT = new ParserDoubleConstant(Double.valueOf(c)); :}
  | MINUS DOUBLE:c
    {: RESULT = new ParserDoubleConstant(-Double.valueOf(c)); :}
  | INF
    {: RESULT = new ParserDoubleConstant(Double.POSITIVE_INFINITY); :}
  | MINUS INF
    {: RESULT = new ParserDoubleConstant(Double.NEGATIVE_INFINITY); :}

    ;


namedconstant ::=
    STRING:s
    {: RESULT = new ParserNamedConstant(s); :}
  | UPPERCASEID:s
    {: RESULT = new ParserNamedConstant(s); :};

